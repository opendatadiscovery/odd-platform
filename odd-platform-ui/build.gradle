plugins {
    id 'java'
    id "com.github.node-gradle.node" version "3.1.0"
    id "org.sonarqube" version "3.3"
}

node {
    download = true
    version = "16.14.0"
    if (env == 'prod') {
        npmInstallCommand = "ci"
    }
}

tasks.register("buildUI", NpmTask) {
    mkdir "${project.buildDir}/ui"
    inputs.files(fileTree("${project.projectDir}/node_modules"))
    inputs.files(fileTree("${project.projectDir}/src"))
    inputs.file("${project.projectDir}/package.json")

    outputs.dir("${project.buildDir}/ui")

    dependsOn npmInstall
    environment = [
        'BUILD_PATH': "${project.buildDir}/ui"
    ]
    args = ['run', 'build']
}

jar {
    dependsOn 'buildUI'
    from "${project.buildDir}/ui" into 'static'
}

sonarqube {
    properties {
        property "sonar.projectKey", "odd-platform_frontend"
        property "sonar.organization", "opendatadiscovery"
        property "sonar.host.url", "https://sonarcloud.io"

        property "sonar.sources", "."

        property "sonar.exclusions", "**/__test?__/**"

        property "sonar.sourceEncoding", "UTF-8"
    }
}