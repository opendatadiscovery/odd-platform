plugins {
    id 'org.springframework.boot' version '2.5.0'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'com.google.cloud.tools.jib' version '3.0.0'
    id 'checkstyle'
}

group = 'com.provectus'
version = "${version != 'unspecified' ? version : '0.0.1-SNAPSHOT'}"

sourceCompatibility = '13'

apply from: 'jooq.generate.gradle'

final boolean skipUIBundle = project.hasProperty("skipUIBundle") &&
        Boolean.parseBoolean(project.property("skipUIBundle").toString())

repositories {
    maven {
        name = "oddGHPRepository"
        url = 'https://maven.pkg.github.com/opendatadiscovery/odd-platform-ingestion-contract'
        credentials(PasswordCredentials)
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.session:spring-session-core'

    implementation "com.provectus.oddplatform:odd-platform-ingestion-contract:$odcIngestionApiVersion"

    implementation project(':odd-platform-api-contract')

    if (!skipUIBundle) {
        implementation project(':odd-platform-ui')
    }

    implementation "org.apache.commons:commons-collections4:$apacheCommonsCollectionsVersion"

    implementation "org.flywaydb:flyway-core:$flywayVersion"
    implementation "org.postgresql:postgresql:$postgresqlDriverVersion"

    implementation "javax.validation:validation-api:$validationApiVersion"
    implementation "org.openapitools:jackson-databind-nullable:$jacksonDatabindVersion"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonJsr310Version"

    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    dbcontainer("org.testcontainers:postgresql:$testContainersVersion")
}

jooq {
    basePackageName = "com.provectus.oddplatform.model"
    includeMatches = ".*"
    excludes = "FLYWAY_SCHEMA_HISTORY"
    imageName = "postgres:13.2-alpine"
    inputSchema = "public"
    generate {
        daos = false
        pojos = true
        fluentSetters = true
        validationAnnotations = true
        pojosEqualsAndHashCode = true
    }
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java'
            srcDirs 'build/generated-jooq/src/main/java'
        }
    }
}

jib {
    from {
        image = 'adoptopenjdk/openjdk13:x86_64-alpine-jre-13.0.2_8'
    }
    to {
        image = 'opendatadiscovery/odd-platform'
        tags = ['latest', project.version]
    }
    container {
        appRoot = '/app'
        jvmFlags = ['-Xms1G', '-Xmx1G']
        ports = ['8080']
        workingDirectory = '/app'
    }
    allowInsecureRegistries = false
}

checkstyle {
    configFile = project(':').file('config/checkstyle/checkstyle.xml')
    configProperties = [ "suppressionFile" : project(':').file('config/checkstyle/suppressions.xml')]
    ignoreFailures = false
    maxWarnings = 0
}

task buildlessCheckstyleMain(type: Checkstyle) {
    configFile = project(':').file('config/checkstyle/checkstyle.xml')
    source 'src/main/java'
    classpath = files()
    configProperties = [ "suppressionFile" : project(':').file('config/checkstyle/suppressions.xml')]
    ignoreFailures = false
    maxWarnings = 0
}

task buildlessCheckstyleTest(type: Checkstyle) {
    configFile = project(':').file('config/checkstyle/checkstyle.xml')
    source 'src/test/java'
    classpath = files()
    configProperties = [ "suppressionFile" : project(':').file('config/checkstyle/suppressions.xml')]
    ignoreFailures = false
    maxWarnings = 0
}


tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled false
    }
}

compileJava.dependsOn tasks.jooqDockerGenerate

test {
    useJUnitPlatform()
}