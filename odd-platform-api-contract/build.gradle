plugins {
    id 'org.openapi.generator' version '5.3.0'
}

dependencies {
    compileOnly "org.springframework:spring-webflux:$springWebfluxVersion"

    compileOnly "javax.validation:validation-api:$validationApiVersion"
    compileOnly "javax.annotation:javax.annotation-api:$annotationApiVersion"
    compileOnly "io.swagger:swagger-annotations:$swaggerAnnotationsVersion"

    compileOnly "io.springfox:springfox-core:$springfoxVersion"
    compileOnly "org.openapitools:jackson-databind-nullable:$jacksonDatabindVersion"
}

openApiGenerate {
    generatorName = "spring"

    inputSpec = "$projectDir/../odd-platform-specification/openapi.yaml"
    outputDir = "$buildDir/generated"

    apiPackage = "org.opendatadiscovery.oddplatform.api.contract.api"
    invokerPackage = "org.opendatadiscovery.oddplatform.api.contract"
    modelPackage = "org.opendatadiscovery.oddplatform.api.contract.model"

    configOptions = [
            dateLibrary  : "java8",
            reactive     : "true",
            interfaceOnly: "true",
            useTags      : "true"
    ]

    globalProperties = [
            supportingFiles: "ApiUtil.java",
            apiTests       : "false",
            models         : "",
            apis           : ""
    ]
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/src/main/java"
        }
    }
}

compileJava.dependsOn tasks.openApiGenerate